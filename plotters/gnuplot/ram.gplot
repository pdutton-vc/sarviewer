#!/usr/bin/gnuplot
#
# Script        :ram.gplot
# Author        :Julio Sanz
# Website       :www.elarraydejota.com
# Email         :juliojosesb@gmail.com
# Description   :Script to create a RAM usage graph with gnuplot
# Dependencies  :gnuplot
# Usage         :gnuplot ram.gplot
# License       :GPLv3
#

reset

# Terminal config
set terminal pngcairo enhanced font 'Verdana,8'
set output '../../graphs/ram.png'
set title "RAM usage graph"
set key bmargin

# Line style
set style line 1 lc rgb '#e72c2c' pt 4 ps 1 lt 1 lw 2 # unavailable (red)
set style line 2 lc rgb '#08b463' pt 4 ps 1 lt 1 lw 2 # available (green)
set style line 3 lc rgb '#ffd07f' pt 4 ps 1 lt 1 lw 2 # used (yellow)
set style line 4 lc rgb '#cccccc' pt 4 ps 1 lt 1 lw 2 # free (grey)
set style line 5 lc rgb '#34bbff' pt 6 ps 1 lt 1 lw 2 # buffers (blue)
set style line 6 lc rgb '#ff77dd' pt 6 ps 1 lt 1 lw 2 # cached (purple)


# Axis configuration
set style line 11 lc rgb '#2c3e50' lt 1 lw 1.5 # Axis line
set border 3 back ls 11
set tics nomirror
set autoscale xy
set xdata time
set timefmt "%H:%M:%S"
set format x "%H:%M"
set xlabel "Time"
set ylabel "Memory (MB)"

# Background grid
set style line 11 lc rgb '#aeb6bf' lt 0 lw 2
set grid back ls 11

# Begin plotting
plot '../../data/ram.dat' using 1:(column(4)/1024-column(6)/1024-column(7)/1024) title 'Unavailable' with l ls 1, \
     ''                   using 1:(column(3)/1024) title 'Available' with l ls 2, \
     ''                   using 1:(column(4)/1024) title 'Used' with l ls 3, \
     ''                   using 1:(column(2)/1024) title 'Free (extraneous)' with l ls 4, \
     ''                   using 1:(column(6)/1024) title 'Buffers' with l ls 5, \
     ''                   using 1:(column(7)/1024) title 'Cached' with l ls 6
