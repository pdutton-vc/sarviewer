#!/usr/bin/gnuplot
#
# Script        :ram.gplot
# Author        :Julio Sanz
# Website       :www.elarraydejota.com
# Email         :juliojosesb@gmail.com
# Description   :Script to create a RAM usage graph with gnuplot
# Dependencies  :gnuplot
# Usage         :gnuplot ram.gplot
# License       :GPLv3
#

reset

# Terminal config
set terminal pngcairo enhanced font 'Verdana,8'
set output '../../graphs/ram.png'
set title "RAM usage graph"
set key bmargin

# Line style
set style line 1 lc rgb '#e74c3c' pt 1 ps 1 lt 1 lw 2 # line1
set style line 2 lc rgb '#3498db' pt 6 ps 1 lt 1 lw 2 # line2
set style line 3 lc rgb '#28b463' pt 4 ps 1 lt 1 lw 2 # line3
set style line 4 lc rgb '#f5b041' pt 4 ps 1 lt 1 lw 2 # line4

# Axis configuration
set style line 11 lc rgb '#2c3e50' lt 1 lw 1.5 # Axis line
set border 3 back ls 11
set tics nomirror
set autoscale xy
set xdata time
set timefmt "%H:%M:%S"
set format x "%H:%M"
set xlabel "Time"
set ylabel "Memory (MB)"

# Background grid
set style line 11 lc rgb '#aeb6bf' lt 0 lw 2
set grid back ls 11

# Begin plotting
# Real free RAM = Free + cached + buffers
# Real used RAM = Used - cached - buffers - free
plot '../../data/ram.dat' using 1:(column(2)/1024+column(5)/1024+column(6)/1024) title 'Free' with l ls 1, \
     ''                   using 1:(column(5)/1024) title 'Buffers' with l ls 2, \
     ''                   using 1:(column(6)/1024) title 'Cached' with l ls 3, \
     ''                   using 1:(column(3)/1024-column(5)/1024-column(6)/1024) title 'Used' with l ls 4
